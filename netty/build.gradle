apply plugin: 'java'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://repo.spring.io/release" }
}

sourceCompatibility = 1.7
group = 'com.github.hvd617.experiments.netty'
version = '1.0.0-SNAPSHOT'


ext {
    jacksonDatabindVersion = '2.2.1'
    jerseyClientVersion = '2.7'
    codaHaleMetricsVersion = '3.0.2'
    nettyVersion = '4.0.17.Final'

    testNgVersion = '6.8'
    hamcrestVersion = '1.3'
    springBootVersion = '1.0.0.RC5'
}


configurations {
    // We will use testng and matcher throughout
    all*.exclude group: 'junit'
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-all'

    // We will use log4j through-out.  Spring boot loads logback by default.
    all*.exclude group: 'commons-logging'
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'

    // We're using jetty instead
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
}

dependencies {
    // Logging
    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    compile 'org.slf4j:jcl-over-slf4j:1.7.5'
    compile 'org.slf4j:jul-to-slf4j:1.7.5'

    compile "com.codahale.metrics:metrics-graphite:${codaHaleMetricsVersion}"
    compile "io.netty:netty-all:${nettyVersion}"


    // Testing
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    testCompile "org.testng:testng:${testNgVersion}"

    // Jersey
    testCompile "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    testCompile "org.glassfish.jersey.core:jersey-client:${jerseyClientVersion}"

    // Spring Boot
    compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile("org.springframework.boot:spring-boot-starter-jetty:${springBootVersion}")
}

test {
    useTestNG()
    include(["**/*Tests.class", "**/*Test.class"])
    exclude(["**/Abstract*.class", '**/*$*'])
}
